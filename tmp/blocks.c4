: cr 13 emit 10 emit ;

: here (ha) @ ;
: last (la) @ ;
: immediate 1 last cell + c! ;
: inline    2 last cell + c! ;

: begin here ; immediate
: while  (jmpnz)  c, , ; immediate
: -while (njmpnz) c, , ; immediate
: until  (jmpz)   c, , ; immediate
: -until (njmpz)  c, , ; immediate

: if (jmpz) c, here 0 , ; immediate
: then here swap ! ; immediate

: num-pad last 32 - ;
: <# num-pad 0 over c! >t ;
: #c t@ 1- dup t! c! ;
: #n '0' + dup '9' > if 7 + then #c ;
: # base @ /mod swap #n ;
: #s begin # -while drop ;
: #> t> ;
: . <# #s #> ztype 32 emit ;

: .2 <# #   #s #> ztype ;
: .3 <# # # #s #> ztype ;
: hex. base @ >t 16 base ! .2 t> base ! ;
: dec. base @ >t 10 base ! .3 t> base ! ;
: bin. base @ >t  2 base ! .  t> base ! ;
27 . cr
last . last hex. last bin. cr
num-pad . num-pad hex. cr
version . cr
cr

: mil 1000 dup * * ;
: bm timer swap for next timer swap - . ;
