; Conway's game of life

1 bL
10 bL

; rR: #rows, rC: #cols, rL: the current line
; r8: the grid, r9: the backup grid
42 sR   150 sC   rR rC * sSZ
rVH s8 rSZ cALLOT
rVH s9 rSZ cALLOT
rVH sL rC 1+ cALLOT

:SETUP rC*# r8+s6 r9+s7 i7;
:ALIVE #2=(\r6C@;) 3=(1;) 0;
:SHOWROW rLs5 1 rC[r7C@ cALIVE #r6C! 10*32+ r5C! 0r7C! i7i6i5];
:SHOW 1 1 cGXY 1 rR[I cSETUP cSHOWROW 0r5C! rL"%s%n"];
:X r4 C@ P r4 C! i4;
:WORKCELL rCs2 r7r2-Ds4 cXcXcX\ r7Ds4 cXi4cX\ r7r2+Ds4 cXcXcX\;
:WORKROW 1 rC[r6 C@(cWORKCELL) i6 i7];
:WORK 1 rR[I cSETUP cWORKROW];
:ONEGEN cSHOW cWORK;
:RANDGRID r9s7 0 rSZ[5 xR r7 C! i7];
:LIFE cCLS cCURSOROFF cRANDGRID 1{\cONEGEN K?~}K@\ cCURSORON;
:RR 2 bL;:ED 2 bE;
