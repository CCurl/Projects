xX

0( Registers )
0( rH: "Here" )
0( rA: Accumulator )
0( rX: X register )
0( rY: Y register )
0( rZ: Zero flag )
0( rN: Negative flag )
0( rC: Carry flag )
0( rP: Program counter )
0( rL: Cycles )

:CODE 0@1[nc@#,59=("%n")];
:RL 0@100+#|6502Asm|\xL;

3000:vMEM;
100:vJT;
0(a--b) :MGET vMEM+c@;
0(b a--):MSET vMEM+c!;
:BCOM 0(b--)  rH MSET iH;
:WCOM 0(w--)  256 & BCOM BCOM;

:LDAI 0(b--) hA9 BCOM BCOM;
:LDXI 0(b--) hA2 BCOM BCOM;
:LDYI 0(b--) hA0 BCOM BCOM;
:DEX  0(--)  hCA BCOM;
:DEY  0(--)  h88 BCOM;
:STAZ 0(b--) h85 BCOM BCOM;
:INCZ 0(n-)  hE6 BCOM BCOM;
:BNE  0(a--) hD0 BCOM rH - d BCOM;
:JMP  0(a--) h4C BCOM WCOM;

:ORG h200; 0(Offset on 6502)
:JTSET  0(a n--) vJT+!;
:JTGET  0(a--n)  vJT+@;
:JTJMP  0(a--)   JTGET#(#q<)\;
256 0[0 n JTSET]
0(--b):NI rPMGETiPiLiL;
:CYC 0(n--) rL+sL;

:SETF 0(n--) #~sZ h80 b& sN;

:_NI #sA SETF;              hA9 JTSET  0(LDAi)
:_NI #sX SETF;              hA2 JTSET  0(LDXi)
:_NI #sY SETF;              hA0 JTSET  0(LDYi)
:_rA NI MSET;               h85 JTSET  0(STAz)
:_dX rX SETF;               hCA JTSET  0(DEX)
:_dY rY SETF;               h88 JTSET  0(DEY)
:_NI s1rZ~(r1 256-rP+sP);   hD0 JTSET  0(BNE)
:_NI vMEM+#c@i#SETF$c!;     hE6 JTSET  0(INCz)
:_NI NI 256*+sP;            h4C JTSET  0(JMP)

:VMINIT ORG sH
    0   LDAI
    8   STAZ
    hff LDXI
rH s1
    hff LDYI
rH s2
    8   INCZ
        DEY
    r2  BNE
        DEX
    r1  BNE
    ORG JMP;

:CR "%n";
:MIL 1000#**;
:REGS rN rZ rY rX rA rP "P:%d A:%d, X:%d Y:%d Z:%d N:%d";
:DUMP rH ORG[n MGET "%x "];

:STS CR REGS rP MGET " (%x)" ;
:EMU 0(n a--) 0sL sP 0[NI JTJMP];
:GO xT$ DUMP ORG EMU CR "8:" 8 MGET . xT$-" (%d ms)";
VMINIT 1 MIL GO
