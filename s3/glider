0(Conway's GOL: a glider gun)

0(register usage:)
0(rA - area A)
0(rB - area B)
0(rI - current cell)
0(rN - count of living neighbors)
0(rC - number of rows)
0(rR - number of columns)
0(rS - size of grid)
0(rX - current cell in area A)
0(rY - current cell in area B)

xX
:RL 1000#|glider|\xL;

0(CODE: show code)
:CODE 0l@1[nc@#,59=(10,)];

0(N: count neighbor: a1--a2)
:N #c@(iN)i;

0(LQ: live?: --f)
:LQ rN2=(rIc@^)rN3=(1^)0;

0(D: display: --)
:D 27,"[H" rBsY rAsX rR 0[rC 0[32 rYc@#rXc!(\'O),iXiY]10,];

0(W: work cell rI: a--f)
:W 0sN rC-d N N N rC+3-N i N rC+3-N N N \LQ;

0(AR: return base for row: n--a)
:AR rC*rB+;

0(CP: copy bytes: to from count--)
:CP l+ s3s2s1 r3 0[r2c@r1c!i1i2] l-;

0(Set variables)
40sC 25sR rRrC*sS 0l@1000+sA rArS+sB 

0(Init the cells)
 0 AR x|........................................|CP
 1 AR x|..............................O.........|CP
 2 AR x|.............................O.O........|CP
 3 AR x|............OO...............OO.O....OO.|CP
 4 AR x|............O.O..............OO.OO...OO.|CP
 5 AR x|...OO..OO......O.............OO.O.......|CP
 6 AR x|...OO.O..O..O..O.............O.O........|CP
 7 AR x|.......OO......O..............O.........|CP
 8 AR x|............O.O.........................|CP
 9 AR x|............OO..........................|CP
10 AR x|........................................|CP
0(Normalize initial data)
rBsI rS 1[0 nrB+ c@ 'O=(i) rI c! iI]

0(Screen stuff)
:CLS 27#,"[2J","[H" D;
:OFF 27,"[?25l";
:ON 27,"[?25h";

0(OG: One generation: --)
:OG rSrC- rCi[rAn+#sI W rBn+c!]D;

0(run X generations or until keypress)
:GO 0[OG k?(k@\xU^)];

CLS OFF 5000 GO ON
xQ
