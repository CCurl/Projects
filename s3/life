0(Conway's game of life)

xX
:CODE 0(Code:--) 0@ 1[n c@ #58=(n 1- c@ 59=("%n"),];

0(ToXY [x y--]: Goto XY)
:TOXY "%e[%d"59,"%dH";
:CUROFF 27"%c[?25l";
:CURON 27"%c[?25h";
:CLS "%e[2J%e[H";
:CR "%n";

0(rR: Rows)
0(rC: Cols)
0(rM: Max Population Size)
:SETROWS 0(n--) sR SETPOPSZ;
:SETCOLS 0(n--) sC SETPOPSZ;
:SETPOPSZ rR 2+ rC 2+ * sM rP rM+ 100+ sB;

0(rP: Population)
0(rB: Working Population)
0(rL: Address of line for output)
2000 sP rP 400- sL

0(T [a--b]: )
:T #c@i%c!i;

0(ALIVE [a--]: Cell is alive)
:ALIVE d# rC- T T T \# T i T \rC+ T T T\;

0(LQ [a b--c]: Live?)
:LQ #2=(\^)3=(\1^)\0;

:RANDPOP rP s0 rM 0[xR hFF b& 200 > r0 c! i0];
:CLRBAK rB s0 rM 0[0 r0 c! i0];
:BAKTOPOP rBs1 rPs2 rM 0[r2c@ r1c@ LQ r2c! 0r1c! i1i2];

0(TOPOP [c r--a])
0(TOBAK [c r--a])
:TOPOP rC*+rP+;
:TOBAK rC*+rB+;

0(PROW [n--]: Print row n)
0(PPOP [--]: Print population)
:PROW 1$TOPOP s1 rLs6 rC1[32r1c@(\'*)r6c!i6i1]0r6c! rLt;
:PPOP 1#TOXY rR 1[n PROW CR];

:PROC 1#TOBAK s4 rC rR TOPOP 1#TOPOP[n c@(r4 ALIVE) i4];
:ONEGEN PROC BAKTOPOP PPOP k?;

25 SETROWS 100 SETCOLS
:GENS 0[ONEGEN r7.i7 (xU^)];
:LIFE 1 s7 CLRBAK RANDPOP CUROFF CLS 1000 GENS CURON;
:RELOAD 1000#|life|\xL;
LIFE
