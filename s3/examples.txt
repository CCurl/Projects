S3 examples:

; To enter a comment: 
    0( here is a comment )
    ; here is another comment
  
;   if (c) { print("Yes") } else { print("No") }
    rC #("Yes")~("No")

 ; x = (a == b) ? c : d;
    rA rB=#(rC$)~(rD)sX;

; To make sure F < T
;   S3 code:             %%>($)
;   Forth equivalent:    OVER OVER > IF SWAP THEN
;   C equivalent:        if (f > t) { int x = f; f = t; t = x; }

; To do something (in this case, execute L) N times:
    1 rN[L]

; Increment Register x, decrement register Y
    iX dY

; To print numbers from F to T:
;   S3 code:             rF rT[rI." "]
;   Forth equivalent:    F @ T @ FOR I . NEXT
;   C equivalent:        for (int i = F; i <= T; i)) { printf("%d ", i); }

; One way to copy N bytes from A to B (n f t--)
    N A B s2 s1 1[r1 c@ r2 c! i1 i2]

; One way to copy N CELLS from A to B (N A B--)
    N A B s2 s1 1[r1 @ r2 ! n1 n2]

; A simple benchmark for a 100 million FOR loop:
    1000#* 100* xT$ 1[] xT$-." ms"

; A simple benchmark for a 100 million WHILE loop:
    1000#* 100* xT$ {1-} xT$-." ms"

; Define a word to display the currently defined code:
    :C xIAU xIH 1-[rI c@ #,';=(rI 1+ c@': =("%n"))];

; Exit S3:
    xQ
